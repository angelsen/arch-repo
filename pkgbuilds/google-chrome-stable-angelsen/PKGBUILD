# Maintainer: Fredrik Angelsen <fredrikangelsen@gmail.com>

pkgname=google-chrome-stable-angelsen
_pkgname=google-chrome-stable
pkgver=140.0.7339.80
pkgrel=2
pkgdesc="Web browser from Google (Stable Channel)"
arch=('x86_64')
url="https://www.google.com/chrome"
license=('custom:chrome')
depends=('alsa-lib' 'gtk3' 'libcups' 'libxss' 'libxtst' 'nss' 'ttf-liberation' 'xdg-utils')
optdepends=('pipewire: WebRTC desktop sharing under Wayland'
    'kdialog: for file dialogs in KDE'
    'gnome-keyring: for storing passwords in GNOME keyring'
    'kwallet: for storing passwords in KWallet')
provides=("google-chrome=$pkgver")
conflicts=('google-chrome')
options=('!strip' '!emptydirs')
install=${pkgname}.install
source=("https://dl.google.com/linux/chrome/deb/pool/main/g/${_pkgname}/${_pkgname}_${pkgver}-1_amd64.deb"
    'google-chrome-stable.sh'
    'eula_text.html')
sha512sums=('c37b05ae25c8184affa52cdf8b1bee4de511bc3c3649a61bc95f963f5ae2ca6207b0dfee7af8ce6deb7ce752e79fb6aaf6c1bfc798b447fd2a5d7060f60a8d88'
            'f2f64bcf0064f027a0ca75a9aa485651ad70a54738d99b930f5f3c701207a777c4f0a9873efe509199e925cd36711a9266e753a8c1ef53fb923e36e65d55f234'
            '2501777a50779c922268f2fc0faa663f07f08f73e3d68453fb16c2eabd360d0bc9ca61cf63e6251eff96fa3cccd0717b5f8be9a2419cc4219763600bd054dea3')

package() {
    # Extract the data from the deb package
    bsdtar -xf data.tar.xz -C "$pkgdir/"

    # Remove the Debian symlink - we'll replace it with our wrapper
    rm "$pkgdir"/usr/bin/google-chrome-stable
    
    # Install our custom launcher that supports user flags
    # This will call Google's wrapper which then calls the actual Chrome binary
    install -m755 google-chrome-stable.sh "$pkgdir"/usr/bin/google-chrome-stable

    # Install the EULA
    install -Dm644 eula_text.html "$pkgdir"/usr/share/licenses/${pkgname}/eula_text.html

    # Remove Debian-specific files
    rm -rf "$pkgdir"/etc/cron.daily
    rm -rf "$pkgdir"/usr/share/menu

    # Fix the desktop entry
    sed -i \
        -e "/Exec=/i\StartupWMClass=Google-chrome" \
        -e "s/x-scheme-handler\/ftp;\?//g" \
        "$pkgdir"/usr/share/applications/google-chrome.desktop
}
